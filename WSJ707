             /****************************************************/
             /* PROGRAM: WSJ707                                  */
             /* AUTHOR: JIM VAN SOEST                            */
             /* DATE: MARCH 21, 2012                             */
             /* DESCRIPTION:                                     */
             /* COMMANDS TO SHUT DOWN THE OnCare Website         */
             /* CORRECTLY.                                       */
             /****************************************************/
             /* MAINTENANCE HISTORY:                             */
             /*                                                  */
             /****************************************************/

             /****************************************************/
             /*                                                  */
             /* PARAMETER OPTIONS:                               */
             /* START - START THE SERVER, SHOULD AREADY BE       */
             /*         STOPPED                                  */
             /* STOP  - STOP THE SERVER, SHOULD BE RUNNING       */
             /* RSTRT - RESTART THE SERVER.  STOP IT AND THEN    */
             /*         START IT.                                */
             /*                                                  */
             /****************************************************/
             PGM        PARM(&ACTION)
             DCL        VAR(&ACTION)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&STOPSVR) TYPE(*CHAR) LEN(200) +
                          VALUE('/QIBM/UserData/WebSphere/AppServer/V+
                          7/ND/profiles/OnCare/bin/stopServer +
                          WebSphere_Portal -profileName OnCare +
                          -username wpsadmin -password popeye10')

             DCL        VAR(&STOPSVR1) TYPE(*CHAR) LEN(200) +
                          VALUE('/QIBM/UserData/WebSphere/AppServer/V+
                          7/ND/profiles/OnCare/bin/stopServer +
                          server1 -profileName OnCare +
                          -username wpsadmin -password popeye10')
             DCL        VAR(&STARTSVR) TYPE(*CHAR) LEN(200) +
                          VALUE('/QIBM/UserData/WebSphere/AppServer/V+
                          7/ND/profiles/OnCare/bin/startServer +
                          WebSphere_Portal -profileName OnCare')
             /*                                                  */
             IF         COND(&ACTION = 'STOP ' | &ACTION = 'RSTRT') +
                          THEN(DO)


               /* Bring down the appserver(s) */
               QSH        CMD(&STOPSVR)
               QSH        CMD(&STOPSVR1)


               /* Bring down the HTTP Server   */
               ENDTCPSVR  SERVER(*HTTP) HTTPSVR(ONCARE)
               MONMSG     MSGID(TCP1A77)

               /* End the document rendering engine */
               ENDJOB     JOB(XVFB4)
             ENDDO

             IF         COND(&ACTION = 'START' | &ACTION = 'RSTRT') +
                          THEN(DO)
               /* START THE WEBSPHERE SUBSYSTEM    */
               STRSBS     SBSD(QWAS7/QWAS7)
               /* CHECK TO SEE IF THE SUBSYSTEM IS ALREADY RUNNING */
               MONMSG     MSGID(CPF1010)

               /* Bring up the WAS server */
               QSH        CMD(&STARTSVR)

               /* Bring up the production HTTP Server */
               STRTCPSVR  SERVER(*HTTP) HTTPSVR(OnCare)
               MONMSG     MSGID(TCP7410)

               /* Start the Document Rendering Engine */

               SBMJOB     CMD(QSYS/CALL PGM(QP2SHELL) +
                            PARM('/usr/bin/X11/X' '-vfb' ':4')) +
                            JOB(XVFB4) JOBD(QGPL/QDFTJOBD) +
                            JOBQ(QSYSNOMAX)
             ENDDO
             ENDPGM
