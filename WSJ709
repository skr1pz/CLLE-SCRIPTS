             /****************************************************/
             /* PROGRAM: WSJ709                                  */
             /* AUTHOR: JIM VAN SOEST                            */
             /* DATE: MARCH 21, 2012                             */
             /* DESCRIPTION:                                     */
             /* COMMANDS TO SHUT DOWN and start the UHSWCM1      */
             /* website correctly.                               */
             /****************************************************/
             /* MAINTENANCE HISTORY:                             */
             /*                                                  */
             /****************************************************/
             /****************************************************/
             /*                                                  */
             /* PARAMETER OPTIONS:                               */
             /* START - START THE SERVER, SHOULD AREADY BE       */
             /*         STOPPED                                  */
             /* STOP  - STOP THE SERVER, SHOULD BE RUNNING       */
             /* RSTRT - RESTART THE SERVER.  STOP IT AND THEN    */
             /*         START IT.                                */
             /*                                                  */
             /****************************************************/
             PGM        PARM(&ACTION)
             DCL        VAR(&ACTION)  TYPE(*CHAR) LEN(5)
             DCL        VAR(&STOPSVR) TYPE(*CHAR) LEN(200) +
                          VALUE('/QIBM/UserData/WebSphere/AppServer/V+
                          61/ND/profiles/UHSWCM1/bin/stopServer +
                          WebSphere_Portal -profileName UHSWCM1 +
                          -timeout 2400 -username wpsadmin +
                          -password popeye10')

             DCL        VAR(&STOPSVR1) TYPE(*CHAR) LEN(200) +
                          VALUE('/QIBM/UserData/WebSphere/AppServer/V+
                          61/ND/profiles/UHSWCM1/bin/stopServer +
                          server1 -profileName UHSWCM1 +
                          -username wpsadmin -password popeye10')

             DCL        VAR(&STARTSVR) TYPE(*CHAR) LEN(200) +
                          VALUE('/QIBM/UserData/WebSphere/AppServer/V+
                          61/ND/profiles/UHSWCM1/bin/startServer +
                          WebSphere_Portal -profileName UHSWCM1')

             IF         COND(&ACTION = 'STOP ' | &ACTION = 'RSTRT') +
                           THEN(DO)
               /* Bring down the appserver(s) */
               QSH        CMD(&STOPSVR)
               QSH        CMD(&STOPSVR1)


               /* Bring down the HTTP Server   */
               ENDTCPSVR  SERVER(*HTTP) HTTPSVR(UHSWCM1)
               MONMSG     MSGID(TCP1A77)

               /* End the document rendering engine */
               ENDJOB     JOB(XVFB1)
             ENDDO

             IF         COND(&ACTION = 'START' | &ACTION = 'RSTRT') +
                          THEN(DO)
               /* START THE WEBSPHERE SUBSYSTEM    */
               STRSBS     SBSD(QWAS61/QWAS61)
               /* CHECK TO SEE IF THE SUBSYSTEM IS ALREADY RUNNING */
               MONMSG     MSGID(CPF1010)

               /* Bring up the portal server */
               /* (server1 isn't needed for running portal) */
               QSH        CMD(&STARTSVR)

               /* Bring up the production HTTP Server */
               STRTCPSVR  SERVER(*HTTP) HTTPSVR(UHSWCM1)
               MONMSG     MSGID(TCP7410)

               /* Start the Document Rendering Engine */

               SBMJOB     CMD(QSYS/CALL PGM(QP2SHELL) +
                            PARM('/usr/bin/X11/X' '-vfb' ':1')) +
                            JOB(XVFB1) JOBD(QGPL/QDFTJOBD) +
                            JOBQ(QSYSNOMAX)
             ENDDO

             ENDPGM
